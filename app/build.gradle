apply plugin: 'com.android.application'

android {
    compileSdkVersion 21
    buildToolsVersion '21.1.1'

    defaultConfig {
        applicationId "com.github.wakhub.tinyclock"
        minSdkVersion 17
        targetSdkVersion 21
        versionCode 3
        versionName "1.2.0"
    }

    signingConfigs {
        release {
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

File signingPropertiesFile = rootProject.file('credentials/release.keystore.properties')
if (signingPropertiesFile.exists()) {
    Properties props = new Properties()
    props.load(new FileInputStream(signingPropertiesFile))
    android.signingConfigs.release.storeFile = file(props.storeFile)
    android.signingConfigs.release.storePassword = props.storePassword
    android.signingConfigs.release.keyAlias = props.keyAlias
    android.signingConfigs.release.keyPassword = props.keyPassword
}
